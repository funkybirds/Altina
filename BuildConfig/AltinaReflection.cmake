include(CMakeParseArguments)

option(AE_ENABLE_REFLECTION "Enable reflection code generation." ON)
option(AE_REFLECTION_FAST_PATH "Skip reflection scanner when no annotations are found." ON)

function(ae_add_reflection_codegen)
    set(options FORBID_ANNOTATIONS)
    set(oneValueArgs TARGET MODULE_NAME MODULE_ROOT)
    set(multiValueArgs SOURCES)
    cmake_parse_arguments(AE_REFL "${options}" "${oneValueArgs}" "${multiValueArgs}" ${ARGN})

    if (NOT AE_REFL_TARGET)
        message(FATAL_ERROR "ae_add_reflection_codegen requires TARGET.")
    endif()

    if (NOT TARGET ${AE_REFL_TARGET})
        message(FATAL_ERROR "ae_add_reflection_codegen target not found: ${AE_REFL_TARGET}.")
    endif()

    if (NOT AE_ENABLE_REFLECTION)
        return()
    endif()

    if (NOT WIN32)
        message(STATUS "Reflection codegen disabled for ${AE_REFL_TARGET}: Windows-only toolchain.")
        return()
    endif()

    if (NOT TARGET AltinaEngineReflectionScanner)
        message(FATAL_ERROR
            "AltinaEngineReflectionScanner target is missing. "
            "Ensure Source/Tools/ReflectionScanner is added before enabling reflection codegen.")
    endif()

    if (NOT AE_REFL_MODULE_NAME)
        set(AE_REFL_MODULE_NAME "${AE_REFL_TARGET}")
    endif()

    if (NOT AE_REFL_MODULE_ROOT)
        get_target_property(AE_REFL_MODULE_ROOT ${AE_REFL_TARGET} SOURCE_DIR)
    endif()
    get_target_property(AE_REFL_TARGET_BINARY_DIR ${AE_REFL_TARGET} BINARY_DIR)

    if (AE_REFL_MODULE_ROOT STREQUAL "AE_REFL_MODULE_ROOT-NOTFOUND" OR AE_REFL_MODULE_ROOT STREQUAL "")
        message(FATAL_ERROR "Failed to resolve module root for ${AE_REFL_TARGET}.")
    endif()

    if (NOT AE_REFL_SOURCES)
        file(GLOB_RECURSE AE_REFL_SOURCES CONFIGURE_DEPENDS
            "${AE_REFL_MODULE_ROOT}/Private/*.c"
            "${AE_REFL_MODULE_ROOT}/Private/*.cc"
            "${AE_REFL_MODULE_ROOT}/Private/*.cpp"
            "${AE_REFL_MODULE_ROOT}/Private/*.cxx"
            "${AE_REFL_MODULE_ROOT}/Private/*.mm"
        )
    endif()

    set(abs_sources "")
    foreach(src IN LISTS AE_REFL_SOURCES)
        if (IS_ABSOLUTE "${src}")
            list(APPEND abs_sources "${src}")
        else()
            list(APPEND abs_sources "${AE_REFL_MODULE_ROOT}/${src}")
        endif()
    endforeach()

    if (abs_sources STREQUAL "")
        message(STATUS "Reflection codegen skipped for ${AE_REFL_MODULE_NAME}: no source files.")
        return()
    endif()

    file(GLOB_RECURSE refl_headers CONFIGURE_DEPENDS
        "${AE_REFL_MODULE_ROOT}/Public/*.h"
        "${AE_REFL_MODULE_ROOT}/Public/*.hpp"
        "${AE_REFL_MODULE_ROOT}/Public/*.hh"
        "${AE_REFL_MODULE_ROOT}/Public/*.hxx"
        "${AE_REFL_MODULE_ROOT}/Public/*.inl"
        "${AE_REFL_MODULE_ROOT}/Private/*.h"
        "${AE_REFL_MODULE_ROOT}/Private/*.hpp"
        "${AE_REFL_MODULE_ROOT}/Private/*.hh"
        "${AE_REFL_MODULE_ROOT}/Private/*.hxx"
        "${AE_REFL_MODULE_ROOT}/Private/*.inl"
    )

    set_property(DIRECTORY APPEND PROPERTY CMAKE_CONFIGURE_DEPENDS ${refl_headers})

    set(gen_dir "${CMAKE_BINARY_DIR}/Generated/${AE_REFL_MODULE_NAME}")
    set(gen_cpp "${gen_dir}/Reflection.gen.cpp")
    set(refl_stub_cpp "${gen_dir}/ReflectionHeaders.cpp")

    set(refl_stub_includes "")
    foreach(header IN LISTS refl_headers)
        file(RELATIVE_PATH header_rel "${AE_REFL_MODULE_ROOT}" "${header}")
        string(REPLACE "\\" "/" header_rel "${header_rel}")
        if (header_rel MATCHES "^Public/")
            string(SUBSTRING "${header_rel}" 7 -1 include_rel)
        elseif (header_rel MATCHES "^Private/")
            string(SUBSTRING "${header_rel}" 8 -1 include_rel)
        else()
            continue()
        endif()
        list(APPEND refl_stub_includes "#include \"${include_rel}\"\n")
    endforeach()
    list(REMOVE_DUPLICATES refl_stub_includes)

    set(refl_stub_content
        "// Auto-generated by CMake. Do not edit.\n"
        "// Module: ${AE_REFL_MODULE_NAME}\n"
        "#include \"Reflection/ReflectionAnnotations.h\"\n"
    )
    list(APPEND refl_stub_content ${refl_stub_includes})
    list(JOIN refl_stub_content "" refl_stub_content_joined)
    file(GENERATE OUTPUT "${refl_stub_cpp}" CONTENT "${refl_stub_content_joined}")
    set_source_files_properties("${refl_stub_cpp}" PROPERTIES GENERATED TRUE)

    set(refl_stub_target "${AE_REFL_TARGET}_ReflectionStub")
    if (NOT TARGET ${refl_stub_target})
        add_library(${refl_stub_target} OBJECT EXCLUDE_FROM_ALL "${refl_stub_cpp}")
        target_include_directories(${refl_stub_target} PRIVATE
            "${AE_REFL_MODULE_ROOT}/Public"
            "${AE_REFL_MODULE_ROOT}/Private"
        )
        target_link_libraries(${refl_stub_target} PRIVATE ${AE_REFL_TARGET})
    endif()

    set(scan_args --file "${refl_stub_cpp}")

    set(modmap_files "")
    set(modmap_dirs "")
    file(RELATIVE_PATH module_rel "${CMAKE_SOURCE_DIR}" "${AE_REFL_MODULE_ROOT}")
    foreach(src IN LISTS abs_sources)
        file(RELATIVE_PATH src_rel "${AE_REFL_MODULE_ROOT}" "${src}")
        set(modmap "${AE_REFL_TARGET_BINARY_DIR}/CMakeFiles/${AE_REFL_TARGET}.dir/${src_rel}.obj.modmap")
        list(APPEND modmap_files "${modmap}")
        get_filename_component(modmap_dir "${modmap}" DIRECTORY)
        list(APPEND modmap_dirs "${modmap_dir}")
    endforeach()
    file(RELATIVE_PATH stub_build_rel "${CMAKE_BINARY_DIR}" "${refl_stub_cpp}")
    file(TO_CMAKE_PATH "${stub_build_rel}" stub_build_rel)
    string(REPLACE "\\" "/" stub_build_rel "${stub_build_rel}")

    string(REPLACE "\\" "/" module_rel_norm "${module_rel}")
    string(REPLACE "/" ";" module_rel_parts "${module_rel_norm}")
    list(LENGTH module_rel_parts module_depth)
    set(stub_prefix "")
    foreach(idx RANGE 1 ${module_depth})
        set(stub_prefix "${stub_prefix}__/")
    endforeach()
    set(stub_modmap_rel "${stub_prefix}${stub_build_rel}")
    set(stub_modmap
        "${AE_REFL_TARGET_BINARY_DIR}/CMakeFiles/${refl_stub_target}.dir/${stub_modmap_rel}.obj.modmap")
    list(APPEND modmap_files "${stub_modmap}")
    get_filename_component(stub_modmap_dir "${stub_modmap}" DIRECTORY)
    list(APPEND modmap_dirs "${stub_modmap_dir}")
    list(REMOVE_DUPLICATES modmap_dirs)

    set(compile_commands_root "${CMAKE_BINARY_DIR}")

    set(has_annotations TRUE)
    if (AE_REFLECTION_FAST_PATH)
        set(has_annotations FALSE)
        foreach(header IN LISTS refl_headers)
            if (NOT EXISTS "${header}")
                continue()
            endif()
            file(READ "${header}" header_text)
            string(REGEX REPLACE "#[ \t]*define[ \t]+ACLASS[^\r\n]*" "" header_text "${header_text}")
            string(REGEX REPLACE "#[ \t]*define[ \t]+APROPERTY[^\r\n]*" "" header_text "${header_text}")
            string(REGEX REPLACE "#[ \t]*define[ \t]+AFUNCTION[^\r\n]*" "" header_text "${header_text}")
            if (header_text MATCHES "ACLASS[ \t\r\n]*\\("
                OR header_text MATCHES "APROPERTY[ \t\r\n]*\\("
                OR header_text MATCHES "AFUNCTION[ \t\r\n]*\\("
                OR header_text MATCHES "\\[\\[AltinaRefl::Class"
                OR header_text MATCHES "\\[\\[AltinaRefl::Property"
                OR header_text MATCHES "\\[\\[AltinaRefl::Function")
                set(has_annotations TRUE)
                break()
            endif()
        endforeach()
    endif()

    if (AE_REFLECTION_FAST_PATH AND has_annotations AND EXISTS "${gen_cpp}")
        file(READ "${gen_cpp}" existing_gen)
        if (existing_gen MATCHES "Auto-generated by CMake \\(no annotations\\)")
            file(REMOVE "${gen_cpp}")
        endif()
    endif()

    if (AE_REFLECTION_FAST_PATH AND NOT has_annotations)
        file(MAKE_DIRECTORY "${gen_dir}")
        if (AE_REFL_FORBID_ANNOTATIONS)
            set(stamp "${gen_dir}/Reflection.forbid.stamp")
            if (NOT EXISTS "${stamp}")
                file(WRITE "${stamp}" "")
            endif()
            set(check_target "${AE_REFL_TARGET}_ReflectionCheck")
            add_custom_target(${check_target} ALL DEPENDS "${stamp}")
            if (NOT AE_REFL_TARGET STREQUAL "AltinaEngineCore")
                add_dependencies(${AE_REFL_TARGET} ${check_target})
            endif()
        else()
            string(MAKE_C_IDENTIFIER "${AE_REFL_MODULE_NAME}" module_ident)
            set(stub_content
                "// Auto-generated by CMake (no annotations). Do not edit.\n"
                "// Module: ${AE_REFL_MODULE_NAME}\n"
                "#include \"Reflection/Reflection.h\"\n"
                "\n"
                "namespace AltinaEngine::Core::Reflection {\n"
                "void RegisterReflection_${module_ident}() {\n"
                "}\n"
                "} // namespace AltinaEngine::Core::Reflection\n"
            )
            list(JOIN stub_content "" stub_content_joined)
            file(GENERATE OUTPUT "${gen_cpp}" CONTENT "${stub_content_joined}")
            set_source_files_properties("${gen_cpp}" PROPERTIES GENERATED TRUE)
            target_sources(${AE_REFL_TARGET} PRIVATE "${gen_cpp}")
        endif()
        return()
    endif()

    if (AE_REFL_FORBID_ANNOTATIONS)
        set(stamp "${gen_dir}/Reflection.forbid.stamp")
        add_custom_command(
            OUTPUT "${stamp}"
            COMMAND ${CMAKE_COMMAND} -E make_directory "${gen_dir}"
            COMMAND ${CMAKE_COMMAND} -E make_directory ${modmap_dirs}
            COMMAND ${CMAKE_COMMAND} -E touch ${modmap_files}
            COMMAND $<TARGET_FILE:AltinaEngineReflectionScanner>
                --compile-commands "${compile_commands_root}"
                --include-headers
                --module-name "${AE_REFL_MODULE_NAME}"
                --module-root "${AE_REFL_MODULE_ROOT}"
                --forbid-annotations
                --strict
                ${scan_args}
            COMMAND ${CMAKE_COMMAND} -E touch "${stamp}"
            DEPENDS
                ${abs_sources}
                ${refl_headers}
                "${refl_stub_cpp}"
                "${compile_commands_root}/compile_commands.json"
                AltinaEngineReflectionScanner
            COMMENT "Checking reflection annotations for ${AE_REFL_MODULE_NAME}"
            COMMAND_EXPAND_LISTS
            VERBATIM
        )
        set(check_target "${AE_REFL_TARGET}_ReflectionCheck")
        add_custom_target(${check_target} ALL DEPENDS "${stamp}")
        if (NOT AE_REFL_TARGET STREQUAL "AltinaEngineCore")
            add_dependencies(${AE_REFL_TARGET} ${check_target})
        endif()
        return()
    endif()

    set(gen_cpp "${gen_dir}/Reflection.gen.cpp")
    add_custom_command(
        OUTPUT "${gen_cpp}"
        COMMAND ${CMAKE_COMMAND} -E make_directory "${gen_dir}"
        COMMAND ${CMAKE_COMMAND} -E make_directory ${modmap_dirs}
        COMMAND ${CMAKE_COMMAND} -E touch ${modmap_files}
        COMMAND $<TARGET_FILE:AltinaEngineReflectionScanner>
            --compile-commands "${compile_commands_root}"
            --include-headers
            --module-name "${AE_REFL_MODULE_NAME}"
            --module-root "${AE_REFL_MODULE_ROOT}"
            --gen-cpp "${gen_cpp}"
            --strict
            ${scan_args}
        DEPENDS
            ${abs_sources}
            ${refl_headers}
            "${refl_stub_cpp}"
            "${compile_commands_root}/compile_commands.json"
            AltinaEngineReflectionScanner
        COMMENT "Generating reflection for ${AE_REFL_MODULE_NAME}"
        COMMAND_EXPAND_LISTS
        VERBATIM
    )

    set_source_files_properties("${gen_cpp}" PROPERTIES GENERATED TRUE)
    target_sources(${AE_REFL_TARGET} PRIVATE "${gen_cpp}")
    add_dependencies(${AE_REFL_TARGET} AltinaEngineReflectionScanner)
endfunction()
